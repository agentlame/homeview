// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
            // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta6'
        classpath 'com.novoda:bintray-release:0.2.7'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


allprojects {
    repositories {
        jcenter()

        maven {
            url 'https://maven.google.com'
            // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
        }    }

    project.ext {
        compileSdkVersion=25
        targetSdkVersion=23
        minSdkVersion=23
        buildToolsVersion='26'
        supportLibraryVersion='26.0.2'
        dexmakerVersion = '1.2'
        mockitoVersion = '1.9.5'
        releaseRepoName = 'exoplayer'
        releaseUserOrg = 'google'
        releaseGroupId = 'com.google.android.exoplayer'
        releaseVersion = 'r2.5.2'
        releaseWebsite = 'https://github.com/google/ExoPlayer'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    compileSdkVersion = 26
    buildToolsVersion = '26'
}

def architectures = [ arm:"armeabi-v7a", x86:"x86" ]

def buildFFmpeg(archAbbrev, arch) {

    exec {

        println "Setting directory : " + "$projectDir/FFmpeg_Android"
        workingDir = "$projectDir/FFmpeg_Android"

        Properties properties = new Properties()
        properties.load(project.file('local.properties').newDataInputStream())
        def ndkDir = properties.getProperty('ndk.dir')
        commandLine './createlibs', archAbbrev, ndkDir, Runtime.runtime.availableProcessors()
    }

    copyFFmpegArchToExoPlayer(archAbbrev, arch)
}

def buildExoPlayerJNI(archs) {

    exec {

        println "Setting directory : " + "$projectDir/ExoPlayer/extensions/ffmpeg/src/main/jni"
        workingDir = "$projectDir/ExoPlayer/extensions/ffmpeg/src/main/jni"

        def procCount = Runtime.runtime.availableProcessors()
        Properties properties = new Properties()
        properties.load(project.file('local.properties').newDataInputStream())
        def ndkDir = properties.getProperty('ndk.dir')
        commandLine "$ndkDir/ndk-build", "APP_ABI=$archs", "-j$procCount"
    }
}

def copyFFmpegLib(archAbbrev, arch) {

    copy {
        from "FFmpeg_Android/android/${archAbbrev}/lib/"
        into "app/src/main/libs/${arch}"
        include '*.a'
        exclude { details ->
            details.file.name.endsWith('.a') &&
                    details.file.text.contains('pkgconfig')
        }
    }
}

def copyFFmpegArchToExoPlayer(archAbbrev, arch) {

    copyFFmpegHeadersExoPlayer(archAbbrev)
    copyFFmpegLibExoPlayer(archAbbrev, arch)
}

def copyFFmpegHeadersExoPlayer(archAbbrev) {

    copy {
        from "FFmpeg_Android/android/${archAbbrev}/include/"
        into "ExoPlayer/extensions/ffmpeg/src/main/jni/ffmpeg"
    }
}

def copyFFmpegLibExoPlayer(archAbbrev, arch) {

    copy {
        from "FFmpeg_Android/android/${archAbbrev}/lib/"
        into "ExoPlayer/extensions/ffmpeg/src/main/jni/ffmpeg/android-libs/${arch}"
        include '*'
        exclude 'pkgconfig'
    }
}

task updateFFmpegSubmodule << {

    println "Updating submodule FFmpeg_Android"
    exec {
        commandLine "git", "submodule", "update", "--init", "--recursive", "FFmpeg_Android"
    }
}

task copyFFmpeg << {

    architectures.each {
        key,value -> copyFFmpegArchToExoPlayer(key,value)
    }

    buildExoPlayerJNI(architectures.values().join(","))
}

task buildFFmpeg <<  {

    updateFFmpegSubmodule

    architectures.each {
        key,value -> buildFFmpeg(key,value)
    }

    buildExoPlayerJNI(architectures.values().join(","))
}
buildFFmpeg.dependsOn updateFFmpegSubmodule
