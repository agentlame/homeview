apply plugin: 'com.android.application'

def appVersionStr = '1.0'
def appVersionInt = 10

if (project.hasProperty('appVersion')) {
    appVersionStr = appVersion
    appVersionInt = (appVersion.toFloat()*10).toInteger()
}

def buildVersion     =           0
def ApiVersion       =     24

def AppVersionShift  =        1000
def ApiVersionShift  =      100000

def BaseVersionCode =    900000000

if (! project.hasProperty('buildNum')) {
    version = "${appVersionStr}.SNAPSHOT"
}
else {
    version = "${appVersionStr}.${buildNum}"
    buildVersion = buildNum.toInteger()
}

appVersionStr = "${appVersionStr}.${buildVersion}"

println "Repo Version : ${version}"
println "App Version : ${appVersionStr} (${appVersionInt})"
println "Build : ${buildVersion}"
println "API : ${ApiVersion}"

android {

    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "com.monsterbutt.homeview"
        minSdkVersion 23
        targetSdkVersion 25
        archivesBaseName = "homeview"

        versionName appVersionStr
        versionCode = BaseVersionCode +
                (ApiVersion * ApiVersionShift) +
                (appVersionInt * AppVersionShift) +
                buildVersion
        println "Version Code (Default) : ${versionCode}"
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            jniDebuggable true
        }
    }


    def Properties props = new Properties()
    def propFile = new File('signing.properties')
    if (propFile.canRead()){
        props.load(new FileInputStream(propFile))

        if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println 'signing.properties found but some entries are missing'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'signing.properties not found'
        android.buildTypes.release.signingConfig = null
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://4thline.org/m2'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile project(':library-core')
    compile project(':library-hls')
    compile project(':library-dash')
    compile project(':library-smoothstreaming')
    compile project(':library-ui')
    compile project(':extension-ffmpeg')

    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:leanback-v17:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile('org.simpleframework:simple-xml:2.7.+') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.android.support:design:25.3.1'

    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    compile group: 'org.fourthline.cling', name: 'cling-core', version:'2.1.1'
    compile group: 'org.fourthline.cling', name: 'cling-support', version:'2.1.1'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.1.12.v20130726'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'8.1.12.v20130726'
    compile group: 'org.eclipse.jetty', name: 'jetty-client', version:'8.1.12.v20130726'

    //compile 'com.google.android.exoplayer:exoplayer:r2.0.3'
}
